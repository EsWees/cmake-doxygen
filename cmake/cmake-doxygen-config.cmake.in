@PACKAGE_INIT@

SET(@PROJECT_NAME@_FOUND TRUE)

IF(NOT Doxygen_FOUND)
	# Handle special case when Doxygen is not present on host machine
	# and REQUIRED keyword is not specified for current module.
	UNSET(@PROJECT_NAME@_FindDoxygen)
	IF(@PROJECT_NAME@_FIND_REQUIRED)
		LIST(APPEND @PROJECT_NAME@_FindDoxygen REQUIRED)
	ENDIF()

	# Passing QUIET to FindDoxygen, if it was specified for current module.
	IF(@PROJECT_NAME@_FIND_QUIETLY)
		LIST(APPEND @PROJECT_NAME@_FindDoxygen QUIET)
	ENDIF()

	FIND_PACKAGE(Doxygen ${@PROJECT_NAME@_FindDoxygen})
	IF(NOT Doxygen_FOUND)
		SET(@PROJECT_NAME@_FOUND FALSE)
		RETURN()
	ENDIF()
ENDIF()

INCLUDE("${CMAKE_CURRENT_LIST_DIR}/cmake-doxygen.cmake")

IF(PLANTUML_PATH)
	SET(@PROJECT_NAME@_plantuml_FOUND TRUE)
ENDIF()

CHECK_REQUIRED_COMPONENTS(@PROJECT_NAME@)

IF(@PROJECT_NAME@_FOUND)
	MESSAGE(STATUS "Found @PROJECT_NAME@: ${CMAKE_CURRENT_LIST_DIR} "
		"(found version \"${@PROJECT_NAME@_VERSION}\")")

	IF(@PROJECT_NAME@_FIND_COMPONENTS)
		MESSAGE(STATUS "Found the following @PROJECT_NAME@ components:")

		FOREACH(comp ${@PROJECT_NAME@_FIND_COMPONENTS})
			MESSAGE(STATUS "   ${comp}")
		ENDFOREACH()
	ENDIF()
ENDIF()
